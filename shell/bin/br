#!/usr/bin/guile \
--no-auto-compile -e main -s
!#

;; br: adjust monitor settings
;; ---------------------------

;; configuration is a list of the following form:
;; (display# contrast-interval brightness-interval)
(define configurations
  '((1 (35 . 50) (0 . 90))))

(define configuration-display car)
(define configuration-features cdr)
(define feature-codes '(12 10))

(define (log v)
  (display v)
  (newline))

(define (build-command display feature value)
  (string-join (list "sudo ddcutil --display"
                     (number->string display)
                     "setvcp"
                     (number->string feature)
                     (number->string value))
               " "))

(define (adjust-settings cfg arg)
  (let ((display (configuration-display cfg)))
    (for-each
     (λ (feature pair)
       (let* ((value (interpolate (car pair) (cdr pair) arg))
              (cmd (build-command display feature value)))
         (log cmd)
         (system cmd)))
     feature-codes
     (configuration-features cfg))))

(define (interpolate a b v)
  (let ((f (/ v 100.0)))
    (inexact->exact (floor (+ a (* f (- b a)))))))

(define (main args)
  (if (not (= (length args) 2))
      (log "usage: br <0-100>")
      (let* ((arg (string->number (cadr args)))
             (adjust (λ (cfg) (adjust-settings cfg arg))))
        (for-each adjust configurations))))
