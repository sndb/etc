#!/usr/bin/guile \
--no-auto-compile -e main -s
!#

(define settings
  '((1 (35 . 50) (0 . 90))
    (2 (35 . 50) (0 . 90))))

(define setting-monitor car)
(define setting-contrast cadr)
(define setting-brightness caddr)

(define feature-brightness 10)
(define feature-contrast 12)

(define (command monitor feature value)
  (map (lambda (x)
         (if (number? x) (number->string x) x))
       `("sudo" "ddcutil" "-d" ,monitor "setvcp" ,feature ,value)))

(define (tweak monitor feature value)
  (display (list monitor feature value))
  (newline)
  (apply system* (command monitor feature value)))

(define (set setting val)
  (let ((monitor (setting-monitor setting)))
    (for-each
     (lambda (feature type)
       (tweak monitor feature (interpolate (type setting) val)))
     (list feature-brightness feature-contrast)
     (list setting-brightness setting-contrast))))

(define (interpolate i v)
  (let ((f (/ v 100.0))
        (min (car i))
        (max (cdr i)))
    (inexact->exact (floor (+ min (* f (- max min)))))))

(define (main args)
  (if (not (= (length args) 2))
      (display "usage: br <0-100>")
      (for-each
       (lambda (s)
         (set s (string->number (cadr args))))
       settings)))
